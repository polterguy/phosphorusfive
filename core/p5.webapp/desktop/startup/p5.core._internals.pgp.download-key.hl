﻿﻿
/*
 * Will download the specified [_arg] key from key server.
 *
 * [_arg] is expected to be the ID of a key.
 */
create-event:p5.core._internals.pgp.download-key

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    _arg:string

  /*
   * Loading key server configuration setting.
   */
  load-file:/desktop/configuration/key-server.hl

  /*
   * Using HTTP GET to query for key on server, making sure we URL encode query.
   */
  p5.html.url-encode:x:/../*/_arg?value
  add:x:/../*/p5.http.xxx
    src:x:/@load-file/*/*/download-key-url/*(!/http-verb!/query-arg-name!/.get-results)
  set:x:/../*/p5.http.xxx?name
    src:p5.http.{0}
      :x:/@load-file/*/*/download-key-url/*/http-verb?value
  p5.http.xxx:"{0}{1}={2}"
    :x:/@load-file/*/*/download-key-url?value
    :x:/@load-file/*/*/download-key-url/*/query-arg-name?value
    :x:/@p5.html.url-encode?value

  /*
   * Converting result from above HTTP GET invocation to a lambda object,
   * such that we can intelligently parse the results, to look for keys.
   *
   * We do this by invoking the [.get-results] in configuration file for key server.
   */
  eval-x:x:/+/*
  eval:x:/@load-file/*/*/download-key-url/*/.get-results
    result:x:/../*/~p5.http./*/result/*/content

  /*
   * Returning results to caller.
   */
  return:x:/@eval?value
