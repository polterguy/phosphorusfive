
/*
 * Supporting Active Event, to create URL from given [_arg] "li" widget.
 */
create-event:sys42.widgets.tree._create-url

  /*
   * First we get current location, without GET parameters, before we add the question mark to make sure we 
   * "open" our GET params collection.
   */
  p5.web.get-location-url
  set:x:/../*/p5.web.get-location-url?value
    src:{0}?
      :x:/../*/p5.web.get-location-url?value

  /*
   * Then we must fetch all GET parameters, and remove any previously created "sys42_widgets_tree_unroll" params,
   * before we add all other GET parameters, to make sure we keep everything EXCEPT "sys42_widgets_tree_unroll".
   */
  p5.web.widgets.find-ancestor:x:/../*/_arg?value
    _root-tree
  p5.web.widgets.property.get:x:/-/*/*?value
    _root-tree
  p5.web.query.list
  set:x:/-/*/{0}
    :x:/../*/p5.web.widgets.property.get/*/*?value
  if:x:/-2/*
    p5.web.query.get:x:/./-2/*?name
    for-each:x:/-/*
      p5.html.url-encode:x:/./*/_dp/#?value
      set:x:/../*/p5.web.get-location-url?value
        src:{0}{1}={2}&
          :x:/../*/p5.web.get-location-url?value
          :x:/..for-each/*/_dp/#?name
          :x:/..for-each/*/p5.html.url-encode?value

  /*
   * Adding key for our "unroll" GET parameter.
   */
  set:x:/../*/p5.web.get-location-url?value
    src:{0}{1}=
      :x:/../*/p5.web.get-location-url?value
      :x:/../*/p5.web.widgets.property.get/*/*?value

  /*
   * Then we need to recursively iterate from current widget, and upwards in hierarchy, until
   * we reach "main root tree widget", and append all IDs to the URL separated by a pipe sign (|).
   * Making sure we p5.html.url-encode the whole thing, before adding it.
   */
  set:x:/+?value
    src:x:/../*/_arg?value
  _cur
  _param
  while:bool:true
    p5.web.widgets.property.get:x:/../*/_cur?value
      _item-id
    set:x:/../*/_param?value
      src:{0}|{1}
        :x:/..while/*/p5.web.widgets.property.get/[0,1]/*/*?value
        :x:/../*/_param?value
    p5.web.widgets.get-parent:x:/../*/_cur?value
    p5.web.widgets.get-parent:x:/-/*/*?value

    /*
     * Checking if we're done. Unless current widget is an "li" widget, we're at the
     * root widget for our tree widget.
     */
    p5.web.widgets.property.get:x:/-/*/*?value
      element
    if:x:/-/*/*?value
      !=:li

      // This is main root tree widget, which is "div" and not "li".
      break
    set:x:/../*/_cur?value
      src:x:/..while/*/p5.web.widgets.get-parent/[1,2]/*/*?value

  /*
   * Now we URL encode entire parameter, before we return it concatenated with base URL to caller.
   */
  p5.string.trim:x:/../*/_param?value
    chars:|
  p5.html.url-encode:x:/../*/p5.string.trim?value

  return:{0}{1}
    :x:/../*/p5.web.get-location-url?value
    :x:/../*/p5.html.url-encode?value
