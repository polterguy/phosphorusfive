## p5.io - File IO in Phosphorus Five

The p5.io library, and its Active Events, allows you to easily load, create, modify, and delete files and folders
in your system. It contains most methods necessary to handle your file system, for most problems you'd encounter
while using P5. Although mostly useful for text files, it also to some extent allows you to manipulate binary files.

Notice, that all IO operations within Phosphorus Five, and its p5.io library, expects the path you supply to start
with a `/`. If what you are referring to is a folder, it also expects you to _end_ your path with a forward slash (/).
Unless you create your paths like this, exceptions will be thrown during evaluation of your code. It also expects
_only_ forward slashes when referenceing files and folders. This is explicitly implemented, to avoid the problems
of backwards slashes versus forward slashes on Linux, xNix, etc. So basically, p5.io will throw an exception, if
you construct paths that would normally only work on one specific type of platform.

Regardless of which platform you are using underneath, you'll have to use forwards slash `/` to separate folders.
This is true for both Windows, Linux and Mac OS.

Also realize, that unless you are authorized to load, save, change, or delete a specific file, or folder, then
a security exception will be thrown. For instance, a user does not by default have access to files belonging to
another user, existing within another user's _"home"_ folder. (e.g. /users/username/some-folder/). If you have
a user named _"foo"_, and you try to load a file from a folder called _"/users/bar/"_, an exception will be
raised, unless your user is a _"root"_ user. These authorisation and authentication features of P5 are
implemented in the p5.io.authorization project, but can easily be exchanged with your own logic, if you need
more fine-grained access control.

Notice also, that all file IO Active Events in p5.io, relies upon the type conversion for loading Hyperlambda,
normally implemented in p5.types. In addition, it will use UTF8 exclusively, as its encoding for files you
save and load. This means that all files created, using p5.io, will be created as UTF8 files. In addition,
all files loaded with p5.io, will be assumed to be encoded as UTF8. This is true for all text files. However,
binary data can still be saved as such. If this does not fit your needs, creating your own loading and saving
events, is easily achieved. In general, at the time of this writing, p5.io exclusively support UTF8 text files,
in addition to some rudimentary support for binary files. Hyperlambda files, are treated as text files.

All Active Events in p5.io, will also automatically substitute a path, with "/users/logged-in-username" if it
starts with `~`. For instance, if you are logged in as username _"root"_, then `~/documents/foo.txt` will
unroll to _"/users/root/documents/foo.txt"_. This allows you to transparently refer to files in a user's
folder as `~/something.txt`, while having p5.io unrolling the exact path automatically for you. This is
one of few exceptions to the rule of that any paths must start with a forwardslash "/". You can also
create your own path events, which will be used by p5.io when unrolling its paths. Below is an example of
creating a path event that unrolls to _"/foo/bar/"_.

```hyperlambda
create-event:p5.io.unroll-path.@FOO
  return:/foo/bar
```

To use the above path substitutor, you could use something like the following.

```hyperlambda
load-file:@FOO/foo.txt
```

This allows you to create paths to files and folders that are treated as _"variables"_ in your system.

**Notice** Although you _can_ load and save binary data with p5.io - Hyperlambda is not
in general terms, very adequate for manipulating binary data. This means that you can load binary data
but for the most parts, the only intelligent thing you can do with it, is to base64 encode this data,
and/or, pass it into other Active Events, that knows how to handle your binary data.

### [load-file], loading files

To load a file, simply use the **[load-file]** Active Event. An example of this event is shown below.

```hyperlambda
load-file:/startup.hl
```

The above invocation will load P5's startup file for you. Notice that this is a Hyperlambda file,
which the **[load-file]** Active Event will automatically determine, and hence parse the file for
you automatically, to become a lambda structure. If you do not wish to automatically parse the file,
but rather load the file _"raw"_, as a piece of text, not transforming it into a lambda object - Then
you must add the argument **[convert]**, and set its value to _"false"_. An example is shown below.

```hyperlambda
load-file:/startup.hl
  convert:false
```

If you evaluate the above Hyperlambda, you will see that it now contains simply a text string,
preserving all comments for you among other things. Unless you explicitly inform the **[load-file]**
Active Event that you do not wish for any conversion to occur, then it will automatically convert all
Hyperlambda for you to lambda objects. This makes it very easy for you to load Hyperlambda, and
immediately evaluate your Hyperlambda, without having to convert it yourself. Loading a Hyperlambda file,
and evaluating it as a lambda object, can be done with two lines of code. Below is an example.

```hyperlambda
load-file:/startup.hl
eval:x:/-/*
```

#### Loading multiple files at the same time

Sometimes, you want to load multiple files at the same time. Often you might even want to treat them as
"one aggregated" file result. For such cases, you can pass in an expression into your **[load-file]**
invocation, such as the following is an example of.

```hyperlambda
_files
  /startup.hl
  /some-other-file.hl
load-file:x:/-/*?name
```

The above code, will load both of the given files, and append them into a node, beneath **[load-file]**,
having the name being the path of the file loaded. The structure will look roughly like this.

```hyperlambda
load-file
  /startup.hl
     /* ... file 1 content, lambda nodes ... */
  /some-other-file.hl
     /* ... file 2 content, lambda nodes ... */
```

To load all Hyperlambda files from a specific folder, and evaluate these files as lambda, the following
code can be used.

```hyperlambda
list-files:/foo/
  filter:.hl
load-file:x:/-/*?name
eval:x:/-/*
```

**Notice**, if you try to load a file that does not exist, an exception will be thrown.

### [save-file], saving files

The **[save-file]** Active Event, does the exact opposite of the **[load-file]** event. Meaning, it saves a
new file, or overwrites an existing file. Try the following code.

```hyperlambda
save-file:~/foo.md
  src:@"Hello there file system!
=======

I am a newly created markdown file!"
```

After evaluating the above Hyperlambda, a new file will exist within your main `~/` user's folder, called _"foo.md"_.
If the file already exists, it will be overwritten.

### [delete-file], deleting one or more files

Just like **[load-file]**, **[delete-file]** can react upon several files at the same time. Its arguments work
the same way as load-file, except of course, instead of loading the file(s), it deletes them. To delete the
files created above in one of our **[save-file]** examples, you can use the following code.

```hyperlambda
delete-file:~/foo.txt
```

Notice, if the above file does not exist, an exception will be thrown.

The Active Event **[delete-file]**, does not take any arguments, besides a single constant value, or an
expression leading to multiple file paths. However, just like the other file manipulation Active Events,
it requires a fully qualified path, which must start with _"/"_. To delete a file, the user context
object must be authorized to modifying the file. Otherwise, an exception will be thrown.

### [file-exists], checking if one or more files exist

**[file-exists]** accepts its arguments the same way **[load-file]** does. However, **[file-exists]** will
return true for each file that exists, instead of returning the content of the file. Example given below.

```hyperlambda
_data
  /startup.hl
  /does-not-exist.txt
file-exists:x:/-/*?name
```

Notice how the above example returns true for the first file, but false for the second file. You can
of course also supply a simple static value, such as the following illustrates.

```hyperlambda
file-exists:~/foo.txt
```

### [move-file], moving or renaming a file

With **[move-file]**, you can either rename a file, or entirely move it into for instance a different folder.
The Active Event takes the source file as its value, and the destinatin filepath/value, as the value of
a **[dest]** child node. Below is an example.

```hyperlambda
save-file:~/foo.txt
  src:foo bar
move-file:~/foo.txt
  dest:~/new-foo.txt
```

If the files you are trying to move or rename, does not exist, an exception will be thrown. If there exist
a file from before, with the same path as the new destination filenames for your file(s), then an exception
will also be thrown, unless you provide an **[overwrite]** argument, and set its value to _"true"_.

### [copy-file], copying a file

The **[copy-file]** Active Event, does exactly what you think it should do. It copies one source file,
and creates a new copy of that file, into a destination file. Besides from that it actually copies the
file(s), instead of moving them, it works 100% identically to **[move-file]**. The arguments to **[copy-file]**
are also the same as the arguments to **[move-file]**. Consider the following code.

```hyperlambda
save-file:~/foo.txt
  src:foo bar
copy-file:~/foo.txt
  dest:~/foo-copy.txt
```

The **[dest]** node argument above, which is the child node of **[copy-file]**, is of course the destination
filepath for your copy.

### Other file events

In addition you also have **[p5.io.file.get-length]**, **[p5.io.file.read-only.get]**, **[p5.io.file.read-only.set]**,
**[p5.io.file.read-only.delete]**, **[p5.io.file.get-last-write-time]** and **[p5.io.file.get-last-access-time]**.
These events does exactly what you'd expect them to do.

```hyperlambda
p5.io.file.get-length:/web.config
p5.io.file.read-only.get:/web.config
p5.io.file.get-last-write-time:/web.config
p5.io.file.get-last-access-time:/web.config
```

After evaluating the above code, your result will look something like this.

```hyperlambda
p5.io.file.get-length
  /web.config:long:8820
p5.io.file.read-only.get
  /web.config:bool:false
p5.io.file.get-last-write-time
  /web.config:date:"2017-06-12T11:02:35"
p5.io.file.get-last-access-time
  /web.config:date:"2017-06-12T22:52:11"
```

### Changing the read-only state of a file

You can set one or more files to read-only with the **[p5.io.file.read-only.set]** Active Event. In addition,
you can remove the read-only attribute, using the **[p5.io.file.read-only.delete]** Active Event. Both of
these Active Events takes either a constant or an expression, leading to multiple files.

### How to handle folders in your system

The Active Events for handling folders, are almost identical to the events for handling files, with some
smaller differences though. Among other things, there obviously does not exist a **[save-folder]** event,
but instead you'll find a **[create-folder]** Active Event, and so on.

### [create-folder]

Creates a folder at the given path. Notice that the parent folder must exist, and that this Active Event does
not recursively create folders. Also notice that if the folder exist from before, an exception will be thrown.
This Active Event also handles expressions, and will create all folders your expressions yields as a result,
the same way for instance the **[load-file]** will load multiple files.

**Important**, every single Active Event that somehow takes a folder, requires the path to both start with a
slash (/), in addition to ending with a slash (/).

```hyperlambda
_folders
  ~/foo/
  ~/bar/
create-folder:x:/-/*?name
```

### [delete-folder]

Delete folder is implemented with the same semantics as **[create-folder]**, except of course, instead of
creating folders, it deletes them. Below is an example.

```hyperlambda
_folders
  ~/foo/
  ~/bar/
delete-folder:x:/-/*?name
```

The above code will delete the folders previously created in our **[create-folder]** example.

### [folder-exists]

This Active Event is implemented with the same semantics as **[file-exists]**, which means if you pass
in an expression as its value, and the expression is leading to multiple folder paths, then it will
return true only for the folders that actually exists.

```hyperlambda
folder-exists:/modules/
```

### [copy-folder] and [move-folder]

These two Active Events works exactly like their file counterparts (**[copy-file]** and **[move-file]**).
Below is some sample code using them both.

```hyperlambda
create-folder:~/foo-bar/
create-folder:~/foo-bar/foo-bar-inner/

// Creating some dummy text file in folder
save-file:~/foo-bar/foo.txt
  src:Foo bar text file
save-file:~/foo-bar/foo-bar-inner/foo2.txt
  src:Foo bar text file

// Then copying the folder we created
copy-folder:~/foo-bar/
  dest:~/foo-bar-2/

// Before finally, we move the original folder we created above.
move-folder:~/foo-bar/
  dest:~/foo-bar-new-name/
```

The above code first creates a folder with an inner folder. Then, for the example, it creates a couple of
files within these two folders. Afterwards, it copies the root folder created like this, before it renames
the original root folder created.

### [list-files] and [list-folders]

These two Active Events, allows you to list files or folders in your system. Both of them can be given either
a constant as a value, or an expression, leading to multiple folder paths. An example is given below.

```hyperlambda
list-files:/modules/
list-folders:/modules/
```

If you evaluate the above Hyperlambda, you will see that these Active Events returns the files and folders,
as their name part of their children nodes. This is a general rule in P5, which is that in general terms,
Active Events that returns a list of strings, returns these as the names of the children nodes of their main
event node.

Notice the **[list-files]** Active Event, can optionally be given a **[filter]**. This is a piece of string,
that each filename must contain, to yield a match. For instance, to list only the Hyperlambda files in your
Micro folder, you could do something like this.

```hyperlambda
list-files:/modules/micro/
  filter:.hl
```

**Notice**, if you start your filter with a period _"."_, then **[list-files]** assumes that you wish to
filter upon file extensions. Otherwise, it will simply retrieve all files somehow containing your specified
search term - Regardless of where this is found in the filename.

### Additional helper Active Events

If you create your own file plugin in C#, that uses paths, you can use the **[.p5.io.unroll-path]** Active Event,
to make sure for instance `~` is replaced with the user's home directory, etc. This Active Event takes a path as
its input, and returns the unrolled path back to caller.

In addition, there exists a pure C# plugin Active Event called **[.p5.io.file.serialize-to-stream]**,
which among other things is used when creating HTTP REST requests through p5.http, to allow serializing files
directly into the HTTP request stream, without loading them into memory first. See the documentation for p5.http
to understand how this works.