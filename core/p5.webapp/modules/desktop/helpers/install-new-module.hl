
/*
 * Allows the user to install a new module, either from a URL, or
 * by browsing to a zip file on his disc.
 */
create-widgets
  micro.widgets.modal:desktop-install-new-module-modal
    widgets
      h3
        innerValue:Install a new module
      p:install-info-paragraph
        innerValue:Browse to a zip file on your disc, containing your module.
      p:install-info-warning
        innerValue:<strong>Warning</strong> - Do not install any modules you don't know for sure are safe, since this can compromise your system.
      div
        class:strip fill
        widgets
          label:install-label-txt
            innerValue:Module
          input:desktop-install-new-module-text
            type:text
            oninit

              /*
               * Setting initial focus to textbox.
               */
              micro.page.set-focus:x:/../*/_event?value

          micro.widgets.upload-button
            accept:.zip
            .onupload

              /*
               * Simply settings textbox' value to path of module uploaded, making
               * sure we rename file in process to original filename.
               *
               * First checking if there exists a file with the same file name in user's
               * temp folder,and deleting it if it does.
               */
              if
                fetch:x:/0/0?value
                  file-exists:~/temp/{0}
                    :x:/../*/files/*/*?value
                delete-file:~/temp/{0}
                  :x:/../*/files/*/*?value

              /*
               * Renaming file.
               */
              move-file:x:/../*/files/*?value
                dest:~/temp/{0}
                  :x:/../*/files/*/*?value

              /*
               * Removing all non-wanted characters from filename, such as versioning information, etc.
               */
              match:x:/../*/files/*/*?value
                src:regex:/[-a-z]+/
              trim:x:/@match/0?name
                chars:-

              /*
               * Setting textbox value to filename, since actual installer logic will determine
               * if user provided a URL or a (local) filename.
               */
              set-widget-property:desktop-install-new-module-text
                .file:x:/../*/files/*/*?value
                value:x:/@trim?value

              /*
               * Setting focus to install button.
               */
              micro.page.set-focus:desktop-install-new-module

              /*
               * Changing info widget's content, and deleting warning widget.
               */
              set-widget-property:install-info-paragraph
                innerValue:Optionally supply a custom <strong>[local-url]</strong>, which becomes your module's name, before you click 'Install'. Don't change this unless you understand what it implies.
              set-widget-property:install-info-warning
                visible:false
              set-widget-property:install-label-txt
                innerValue:Local URL

          button:desktop-install-new-module
            innerValue:Install
            onclick

              /*
               * Making sure we create an obscurer, before we invoke the actual implementation.
               */
              create-widgets
                micro.widgets.cover:desktop-installation-cover-widget
                  message:Please wait while your application is installed ...

              /*
               * Creating a pingback, which does the actual heavy lifting.
               */
              micro.lambda.create-timeout
                milliseconds:1
                onfinish

                  /*
                   * Invoking Ajax method that is actually responsible for installing our module.
                   *
                   * Making sure we wrap invocation in a try/catch block.
                   */
                  try
                    p5.web.widgets.ajax-events.raise:desktop-install-new-module
                      .onclick
                  catch

                    /*
                     * Oops, exception ocurred.
                     */
                    micro.windows.info:That module couldn't be installed for some reasons
                      class:micro-windows-info warning

            .onclick

              /*
               * Retrieving textbox value, and doing basic sanity check.
               */
              get-widget-property:desktop-install-new-module-text
                value
                .file
              if:x:/@get-widget-property/*/*/value?value
                =:

                /*
                 * Oops, no module supplied ...
                 * Warning user, and returning early.
                 */
                micro.windows.info:You need to supply a module
                  class:micro-windows-info warning
                micro.page.set-focus:desktop-install-new-module-text
                return

              /*
               * Installing module.
               */
              eval-x:x:/+/*/local-url
              desktop.modules.install:~/temp/{0}
                :x:/@get-widget-property/*/*/.file?value
                local-url:x:/@get-widget-property/*/*/value?value

              /*
               * Reloading desktop, to have icons re-created.
               *
               * However, before we can reload location, we'll need to make
               * sure user is notified on next pageload that uninstallation was a success.
               */
              desktop.evaluate.on-next-pageload
                lambda
                  micro.windows.info:Your module was successfully installed
                    class:micro-windows-info success
              p5.web.reload-location

      div
        class:right
        widgets
          button:desktop-install-new-module-cancel
            innerValue:Cancel
            onclick

              /*
               * Simply deletes modal window.
               */
              delete-widget:desktop-install-new-module-modal
