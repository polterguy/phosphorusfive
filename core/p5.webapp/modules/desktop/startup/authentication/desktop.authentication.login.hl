
/*
 * File responsible for creating our [desktop.authentication.login] event, which simply displays a modal widget,
 * allowing the user to log into system.
 *
 * Pass in [message] to give the user a clue about why he has to log in.
 */
create-event:desktop.authentication.login

  /*
   * Displays a modal window allowing the user to login.
   */
  create-widgets
    micro.widgets.modal:desktop-login
      widgets
        h3
          innerValue:Please login

        /*
         * Using a wizard widget for simplicity.
         */
        micro.widgets.wizard-form:desktop-wizared-form

          /*
           * Username widget
           */
          text:desktop-username
            info:Username
            .data-field:username
            onkeydown:@"if (event.keyCode == 13) {p5.$('desktop-login-button').raise('onclick');return false;}"
            oninit

              /*
               * Setting initial focus to username textbox.
               */
              micro.page.set-focus:x:/../*/_event?value

          /*
           * Password widget
           */
          text:desktop-password
            info:Password
            .data-field:password
            type:password
            onkeydown:@"if (event.keyCode == 13) {p5.$('desktop-login-button').raise('onclick');return false;}"

          collection:p5-core-change-pwd-wrapper
            visible:false
            text:p5-core-new-pwd
              .data-field:new-pwd
              info:New pwd
              type:password
              autocomplete:new-password
              onkeydown:@"if (event.keyCode == 13) {p5.$('desktop-login-button').raise('onclick');return false;}"
            text
              .data-field:new-pwd-repeat
              info:Repeat
              type:password
              autocomplete:new-password
              onkeydown:@"if (event.keyCode == 13) {p5.$('desktop-login-button').raise('onclick');return false;}"

          /*
           * Remember me checkbox.
           */
          div
            widgets
              label
                widgets
                  span
                    innerValue:Remember me
                  input
                    type:checkbox
                    .data-field:persist
                    checked
                    title:Only check this box if you are using your own client, such as your personal computer or phone
              label
                widgets
                  span
                    innerValue:Change password
                  input
                    type:checkbox
                    .data-field:change
                    title:Check this box if you'd like to change your existing password
                    onchange

                      /*
                       * Showing or hiding "supply new password wrapper", depending upon whether or not checkbox
                       * was actually checked or not.
                       */
                      get-widget-property:x:/../*/_event?value
                        checked
                      if:x:/@get-widget-property/*/*

                        /*
                         * Showing "change password" wrapper.
                         */
                        set-widget-property:p5-core-change-pwd-wrapper
                          visible:true
                        micro.page.set-focus:p5-core-new-pwd

                      else

                        /*
                         * Hiding "change password" wrapper.
                         */
                        set-widget-property:p5-core-change-pwd-wrapper
                          visible:false
                        micro.page.set-focus:desktop-username

              button:desktop-login-button
                innerValue:Login
                style:"float:right;"
                onclick

                  /*
                   * Retrieving username and password, and attempting to log user into system.
                   */
                  micro.form.serialize:desktop-wizared-form
                  add:x:/../*/try/*/login
                    src:x:/@micro.form.serialize/*(/username|/password|/persist)

                  /*
                   * Wrapping login attempt in try/catch block, since login throws if login is unsuccessful.
                   */
                  try

                    /*
                     * Now that we have parametrised our invocation, we can attempt to login.
                     */
                    login

                    /*
                     * Checking if user requested to have his password changed.
                     */
                    if:x:/@micro.form.serialize/*/change?value

                      /*
                       * Verifying passwords are a match before proceeding.
                       */
                      if:x:/@micro.form.serialize/*/new-pwd?value
                        !=:x:/@micro.form.serialize/*/new-pwd-repeat?value

                        /*
                         * Warning user, and aborting the rest of the evaluation of the current lambda.
                         */
                        micro.windows.info:Passwords didn't match
                          class:micro-windows-info warning
                        micro.page.set-focus:p5-core-new-pwd
                        return

                      /*
                       * Changing user's password.
                       */
                      p5.auth.misc.change-my-password:x:/@micro.form.serialize/*/new-pwd?value

                    /*
                     * If we come this far, without an exception, our login attempt was successful, and
                     * we can reload URL for simplicity.
                     */
                    p5.web.reload-location

                  catch

                    /*
                     * Displaying error to user.
                     */
                    micro.windows.info:x:/..catch/*/message?value
                      class:micro-windows-info warning





  /*
   * Checking if caller supplied a message, and if so, making sure we alert user
   */
  if:x:/../*/message?value

    /*
     * Caller supplied a message, making sure we apply it.
     */
    micro.windows.info:x:/../*/message?value
      class:micro-windows-info warning
